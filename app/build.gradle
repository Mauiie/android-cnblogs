apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

// 加载配置文件
final Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)

// 基本配置信息，签名请替换您自己的签名文件，建议在local.properties里面配置。
def signKeyStorePassword = properties.getProperty("keystore.keyPassword", "cnblogs")
def signStorePassword = properties.getProperty("keystore.storePassword", "cnblogs")
def signStoreFile = properties.getProperty("keystore.file", "debug.keystore")
def umengAppKey = properties.getProperty("umengAppKey", "replace_your_umeng_key")
def buglyAppId = properties.getProperty("buglyAppId", "replace_your_bugly_appid")
def leancloudAppId = properties.getProperty("leancloudAppId", "replace_your_leancloud_appid")
def leancloudAppKey = properties.getProperty("leancloudAppKey", "replace_your_leancloud_appeky")


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    dexOptions {
        javaMaxHeapSize '2g'
    }

    signingConfigs {
        release {
            keyAlias 'com.rae.cnblogs'
            storeFile file(signStoreFile)
            keyPassword signKeyStorePassword
            storePassword signStorePassword
        }
    }

    defaultConfig {
        applicationId "com.rae.cnblogs"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'BUGLY_APP_ID', '"' + buglyAppId + '"'
        buildConfigField 'String', 'LEAN_CLOUD_APP_ID', '"' + leancloudAppId + '"'
        buildConfigField 'String', 'LEAN_CLOUD_APP_KEY', '"' + leancloudAppKey + '"'
        manifestPlaceholders = [
                UMENG_APPKEY: umengAppKey
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix '.debug'
            manifestPlaceholders = [
                    UMENG_CHANNEL: 'DEV'
            ]
            signingConfig signingConfigs.release
        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }

    // 渠道包定义，默认定义的名称就是渠道名称
    productFlavors {
        DEV {}          // 测试
        baidu {}        // 百度手机助手
        yinyongbao {}   // 应用宝
//        m360 {}         // 360手机助手
//        pp {}           // PP助手
        xiaomi {}       // 小米商店
        huawei {}       // 华为商店
        official {}      // 官方版本
    }

    // 批量渠道包值替换
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL: name,BUGLY_CHANNEL:name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:design:${rootProject.ext.supportVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    compile 'com.android.support:multidex:1.0.1'
    compile project(':sdk')
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'com.github.raee:XRecyclerView:1.3.2'
    compile 'com.makeramen:roundedimageview:2.3.0'
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    compile 'com.umeng.analytics:analytics:6.1.2'
    // 事件通知
    compile 'org.greenrobot:eventbus:3.0.0'
    // 滑动返回
    compile 'me.imid.swipebacklayout.lib:library:1.0.0'
    compile 'com.github.chrisbanes:PhotoView:2.1.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.1'
    // 非常强悍的RecyclerView
    compile 'eu.davidea:flexible-adapter:5.0.0-rc2'
    // LeanCloud 基础包
    compile 'cn.leancloud.android:avoscloud-sdk:v4.4.3'
    // LeanCloud 用户反馈包
    compile 'cn.leancloud.android:avoscloud-feedback:v4.4.3@aar'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    // 主题切换
    compile 'skin.support:skin-support:2.1.2'
    compile 'skin.support:skin-support-design:1.2.5'

    compile 'com.kyleduo.switchbutton:library:1.4.6'
}

// 热更新
if (enableHotfix.toBoolean()) {
    apply from: 'tinker-support.gradle'
}
