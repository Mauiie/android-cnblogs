apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

// 加载配置文件
Properties properties = new Properties()
InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
properties.load(inputStream)

def signKeyStorePassword = properties.getProperty("keystore.keyPassword")
def signStorePassword = properties.getProperty("keystore.storePassword")
def signStoreFile = properties.getProperty("keystore.file")
def umengAppKey = properties.getProperty("umengAppKey")
def buglyAppId = properties.getProperty("buglyAppId")
def leancloudAppId = properties.getProperty("leancloudAppId")
def leancloudAppKey = properties.getProperty("leancloudAppKey")


android {
    compileSdkVersion 25
    buildToolsVersion '26.0.0'

    dexOptions {
        javaMaxHeapSize '2g'
    }

    signingConfigs {
        release {
            keyAlias 'com.rae.cnblogs'
            storeFile file(signStoreFile)
            keyPassword signKeyStorePassword
            storePassword signStorePassword
        }
    }

    defaultConfig {
        applicationId "com.rae.cnblogs"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'BUGLY_APP_ID', '"' + buglyAppId + '"'
        buildConfigField 'String', 'LEAN_CLOUD_APP_ID', '"' + leancloudAppId + '"'
        buildConfigField 'String', 'LEAN_CLOUD_APP_KEY', '"' + leancloudAppKey + '"'
        manifestPlaceholders = [
                UMENG_APPKEY : umengAppKey,
                UMENG_CHANNEL: 'DEV'
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:design:${rootProject.ext.supportVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    compile 'com.android.support:multidex:1.0.1'
    compile project(':sdk')
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    compile 'in.srain.cube:ultra-ptr:1.0.11'
    compile 'com.github.raee:XRecyclerView:1.3.2'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.umeng.analytics:analytics:6.1.1'
    // 事件通知
    compile 'org.greenrobot:eventbus:3.0.0'
    // 滑动返回
    compile 'me.imid.swipebacklayout.lib:library:1.0.0'
    compile 'com.github.chrisbanes:PhotoView:2.1.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.1'
    // 非常强悍的RecyclerView
    compile 'eu.davidea:flexible-adapter:5.0.0-rc2'
    // LeanCloud 基础包
    compile 'cn.leancloud.android:avoscloud-sdk:v4.4.3'
    // LeanCloud 用户反馈包
    compile 'cn.leancloud.android:avoscloud-feedback:v4.4.3@aar'
}

// 热更新
if (enableHotfix.toBoolean()) {
    apply from: 'tinker-support.gradle'
}
